#!@PERL@
# BEGIN BPS TAGGED BLOCK {{{
#
# COPYRIGHT:
#
# This software is Copyright (c) 1996-2022 Best Practical Solutions, LLC
#                                          <sales@bestpractical.com>
#
# (Except where explicitly superseded by other copyright notices)
#
#
# LICENSE:
#
# This work is made available to you under the terms of Version 2 of
# the GNU General Public License. A copy of that license should have
# been provided with this software, but in any event can be snarfed
# from www.gnu.org.
#
# This work is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
# 02110-1301 or visit their web page on the internet at
# http://www.gnu.org/licenses/old-licenses/gpl-2.0.html.
#
#
# CONTRIBUTION SUBMISSION POLICY:
#
# (The following paragraph is not intended to limit the rights granted
# to you to modify and distribute this software under the terms of
# the GNU General Public License and is only of importance to you if
# you choose to contribute your changes and enhancements to the
# community by submitting them to Best Practical Solutions, LLC.)
#
# By intentionally submitting any modifications, corrections or
# derivatives to this work, or any other work intended for use with
# Request Tracker, to Best Practical Solutions, LLC, you confirm that
# you are the copyright holder for those contributions and you grant
# Best Practical Solutions,  LLC a nonexclusive, worldwide, irrevocable,
# royalty-free, perpetual, license to use, copy, create derivative
# works based on those contributions, and sublicense and distribute
# those contributions and any derivatives thereof.
#
# END BPS TAGGED BLOCK }}}
use strict;
use warnings;

# fix lib paths, some may be relative
BEGIN { # BEGIN RT CMD BOILERPLATE
    require File::Spec;
    require Cwd;
    my @libs = ("@RT_LIB_PATH@", "@LOCAL_LIB_PATH@");
    my $bin_path;

    for my $lib (@libs) {
        unless ( File::Spec->file_name_is_absolute($lib) ) {
            $bin_path ||= ( File::Spec->splitpath(Cwd::abs_path(__FILE__)) )[1];
            $lib = File::Spec->catfile( $bin_path, File::Spec->updir, $lib );
        }
        unshift @INC, $lib;
    }

}

use RT::Interface::CLI qw(Init);
my %opt;
Init(\%opt, 'from=s', 'force');

unless ( $opt{'from'} ) {
    print STDERR "Missing 'from' argumnet\n";
    RT::Interface::CLI->ShowHelp();
    exit(1);
}

my $attrs = RT::Attributes->new(RT->SystemUser);
$attrs->Limit( FIELD => 'Name', VALUE => 'SavedSearch' );

while ( my $attr = $attrs->Next ) {
    my $content = $attr->Content or next;
    my $format = $content->{Format} or next;
    if ( $format =~ s!(href=["'])$opt{from}!$1__WebPath__!g ) {
        my $indent_old = $content->{Format};
        my $indent_new = $format;
        $indent_old =~ s!^!    !mg;
        $indent_new =~ s!^!    !mg;
        print "Update format\n$indent_old\nto\n$indent_new\n" unless $opt{force};
        if ( $opt{force} || _yesno() ) {
            $content->{Format} = $format;
            my ($ret, $msg) = $attr->SetContent($content);
            if ( $ret ) {
                print "Updated format\n$indent_old\nto\n$indent_new\n";
            }
            else {
                RT->Logger->error("Failed to update Format of SavedSearch #" . $attr->id . ": $msg");
            }
        }
    }
}

sub _yesno {
    print "Proceed [y/N]:";
    my $x = scalar(<STDIN>);
    $x =~ /^y/i;
}

__END__

=head1 NAME

upgrade-web-path - Upgrade WebPath in format of saved searches

=head1 SYNOPSIS

     upgrade-web-path --from /rt
     upgrade-web-path --from /rt --force

=head1 DESCRIPTION

This script updates URLs with old L<RT_Config/$WebPath> in format of saved searches.

=head1 OPTIONS

=over 4

=item C<--from>

The old L<RT_Config/$WebPath> value, which will be replaced with C<__WebPath__>.

=back

=cut
